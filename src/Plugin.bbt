REM >!RunImage
REM
REM Copyright 2001-2014, Stephen Fryatt (info@stevefryatt.org.uk)
REM
REM This file is part of WinKeys:
REM
REM   http://www.stevefryatt.org.uk/software/
REM
REM Licensed under the EUPL, Version 1.1 only (the "Licence");
REM You may not use this work except in compliance with the
REM Licence.
REM
REM You may obtain a copy of the Licence at:
REM
REM   http://joinup.ec.europa.eu/software/page/eupl
REM
REM Unless required by applicable law or agreed to in
REM writing, software distributed under the Licence is
REM distributed on an "AS IS" basis, WITHOUT WARRANTIES
REM OR CONDITIONS OF ANY KIND, either express or implied.
REM
REM See the Licence for the specific language governing
REM permissions and limitations under the Licence.
:
REM Windows Keys Setup
:
LIBRARY "BASIC:WimpLib"
LIBRARY "BASIC:Icon"
LIBRARY "BASIC:String"
LIBRARY "BASIC:Template"
LIBRARY "BASIC:Window"
:
ON ERROR VDU 4 : PRINT REPORT$;" at line ";ERL : END
:
PROCinitialise
:
ON ERROR quit%=(FNerror(task_name$)=2)
:
WHILE NOT quit%
 PROCpoll
ENDWHILE
:
PROCterminate
:
END
:
:
:
:
REM
:
:
:
:
DEF PROCpoll
SYS "Wimp_Poll",&3831,b% TO reason%

CASE reason% OF
 WHEN 2     : SYS "Wimp_OpenWindow",,b%
 WHEN 3     : SYS "Wimp_CloseWindow",,b%
 WHEN 6     : PROCmouse_click
 WHEN 8     : PROCkey_press
 WHEN 17,18 : PROCwimp_message
ENDCASE
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCmouse_click
IF b%!12 = settings_window% THEN
 CASE b%!16 OF
  WHEN 0 : PROCwrite_configuration
           IF b%!8=4 THEN quit%=TRUE
  WHEN 1 : IF b%!8=4 THEN quit%=TRUE ELSE PROCfill_window : PROCredraw_window
  WHEN 2 : IF b%!8=4 THEN PROCclear_window : PROCredraw_window
 ENDCASE
ENDIF
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCkey_press
CASE b%!24 OF
 WHEN 13   : PROCwrite_configuration : quit%=TRUE
 WHEN 27   : quit%=TRUE
 OTHERWISE : SYS "Wimp_ProcessKey",b%!24
ENDCASE
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCwimp_message
CASE b%!16 OF
 WHEN 0      : quit%=TRUE
 WHEN 3      : PROCmessage_dataload(b%!24,FNstring_read((b%+44),b%!40)
 WHEN &502   : PROCsend_interactive_help
 WHEN &50D83 : PROCwindow_open(settings_window%)
ENDCASE
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCinitialise
:
LOCAL wimp_version%, wimp_required%, flags%, size%
:
REM Load the application's message file.
:
DIM message_file% 16
SYS "MessageTrans_FileInfo",,"<WinKeys$Dir>.Messages" TO flags%,,size%
DIM f% size%
SYS "MessageTrans_OpenFile",message_file%,"<WinKeys$Dir>.Messages",f%
:
DIM b% &4000, q% 255, a% 255
:
$b%="TASK"
TASK=!b%
:
task_name$=FNmessage_lookup("_TaskName")
wimp_required%=310
:
REM Identify which Wimp Messages we wish to claim.
:
q%!0=&50D83 : REM Message_OpenConfigWindow
q%!4=&502   : REM Message_HelpRequest
q%!8=3      : REM Message_DataLoad
q%!&C=0     : REM Message_Quit
:
SYS "Wimp_Initialise",wimp_required%,TASK,task_name$,q% TO wimp_version%,task_handle%
:
quit%=FALSE
:
indirection_size%=3000
DIM indirection_data% indirection_size%
:
PROCtemplate_open("<WinKeys$Dir>.Templates")
PROCtemplate_load("Setup",b%,indirection_data%,indirection_size%,-1)
SYS "Wimp_CreateWindow",,b% TO settings_window%
PROCtemplate_close
:
DIM key_name$(7), key_icon%(7)
:
key_name$()="Win","ShiftWin","CtrlWin","ShiftCtrlWin","Menu","ShiftMenu","CtrlMenu","ShiftCtrlMenu"
key_icon%()=5,7,9,11,14,16,18,20
:
PROCfill_window
:
SYS "OS_GetEnv" TO command_line$
inset%=INSTR(command_line$,"-openat")
IF inset%>0 THEN
 inset_x%=INSTR(command_line$," ",inset%)
 inset_y%=INSTR(command_line$," ",inset_x%+1)
 x%=VAL(MID$(command_line$,inset_x%+1,INSTR(command_line$," ",inset_x%+1)-inset_x%))
 y%=VAL(MID$(command_line$,inset_y%+1,INSTR(command_line$," ",inset_y%+1)-inset_y%))
 PROCopen_window_at(settings_window%,x%,y%)
ELSE
 PROCwindow_open(settings_window%)
ENDIF
PROCicon_put_caret_at_end(settings_window%,key_icon%(0))
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCterminate
:
SYS "Wimp_CloseDown",task_handle%,TASK
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCfill_window
:
LOCAL loop%
:
FOR loop%=0 TO 7
 SYS "XOS_ReadVarVal","WinKeys$"+key_name$(loop%),b%,4000,0,0 TO ,,length%
 b%?length%=13
 $FNicon_indirection(settings_window%,key_icon%(loop%))=$b%
NEXT loop%
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCwrite_configuration
:
LOCAL loop%,value$,length%,filename$,handle%,type%
:
filename$="<Choices$Write>.WinKeys"
SYS "OS_File",17,filename$ TO type%
IF type%=0 THEN SYS "OS_File",8,filename$,,,0
filename$+=".SetKeys"
handle%=OPENOUT(filename$)
IF handle%<>0 THEN
 BPUT#handle%,"| WinKeys Setup File"
 BPUT#handle%,"|"
 BPUT#handle%,"| This file was autogenerated by WinKey Setup."
 BPUT#handle%,""
ENDIF
:
FOR loop%=0 TO 7
 value$=$FNicon_indirection(settings_window%,key_icon%(loop%))
 IF value$="" THEN
  length%=-1 : REM -1 deletes system variable if no value is present.
 ELSE
  length%=LEN(value$)
  IF handle%<>0 THEN BPUT#handle%,"Set "+"WinKeys$"+key_name$(loop%)+" "+value$
 ENDIF
 SYS "XOS_SetVarVal","WinKeys$"+key_name$(loop%),value$,length%,0,0
NEXT loop%
:
IF handle%<>0 THEN
 CLOSE#handle%
 SYS "OS_File",18,filename$,&FEB
ENDIF
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCclear_window
:
LOCAL loop%
:
FOR loop%=0 TO 7
 $FNicon_indirection(settings_window%,key_icon%(loop%))=""
NEXT loop%
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCredraw_window
:
LOCAL loop%
:
FOR loop%=0 TO 7
 PROCicon_redraw(settings_window%,key_icon%(loop%))
NEXT loop%
:
SYS "Wimp_GetCaretPosition",,q%
IF !q%=settings_window% THEN PROCicon_put_caret_at_end(settings_window%,q%!4)
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCmessage_dataload(icon%,filename$,filetype%)
:
LOCAL loop%,found%,position%
:
found%=FALSE
FOR loop%=0 TO 7
 IF key_icon%(loop%)=icon% THEN found%=TRUE
NEXT loop%
:
IF found% THEN
 position%=-1
 :
 CASE filetype% OF
  WHEN &1000 : filename$="Filer_OpenDir "+filename$
  WHEN &2000 : filename$="StartDesktopTask "+filename$
  OTHERWISE
   SYS "Wimp_GetCaretPosition",,q%
   IF !q%=settings_window% AND q%!4=icon% THEN
    position%=q%!20+LEN(filename$)
    filename$=LEFT$($FNicon_indirection(settings_window%,icon%),q%!20)+filename$+MID$($FNicon_indirection(settings_window%,icon%),q%!20+1)
   ELSE
    filename$=$FNicon_indirection(settings_window%,icon%)+filename$
   ENDIF
 ENDCASE
 :
 $FNicon_indirection(settings_window%,icon%)=LEFT$(filename$,&FE)
 PROCicon_redraw(settings_window%,icon%)
 IF FNicon_test_for_caret(settings_window%,icon%) THEN
  IF position%=-1 THEN position%=LEN($FNicon_indirection(settings_window%,icon%))
  SYS "Wimp_SetCaretPosition",settings_window%,icon%,,,-1,position%
 ENDIF
ENDIF
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF PROCsend_interactive_help
:
LOCAL help_token$,key_token$,length%
:
help_token$=""
key_token$=""
:
IF b%!32=settings_window% THEN
 CASE b%!36 OF
  WHEN 0,1,2 : help_token$="Help0"+STR$(b%!36)
  WHEN 5     : help_token$="HelpKey" : key_token$="KeyW"
  WHEN 7     : help_token$="HelpKey" : key_token$="KeySW"
  WHEN 9     : help_token$="HelpKey" : key_token$="KeyCW"
  WHEN 11    : help_token$="HelpKey" : key_token$="KeySCW"
  WHEN 14    : help_token$="HelpKey" : key_token$="KeyM"
  WHEN 16    : help_token$="HelpKey" : key_token$="KeySM"
  WHEN 18    : help_token$="HelpKey" : key_token$="KeyCM"
  WHEN 20    : help_token$="HelpKey" : key_token$="KeySCM"
 ENDCASE
ENDIF
:
IF help_token$<>"" THEN
 IF key_token$<>"" THEN SYS "MessageTrans_Lookup",message_file%,key_token$,q%,&FF ELSE $q%=""
 SYS "MessageTrans_Lookup",message_file%,help_token$,b%+20,235,q% TO,,,length%
 b%!12=b%!8
 b%!16=&503
 !b%=24+(length%AND&FFFFFC)
 SYS "Wimp_SendMessage",17,b%,b%!4
ENDIF
ENDPROC
:
:
:
:
REM
:
:
:
:
DEF FNmessage_lookup(token$)
:
LOCAL message$
:
IF token$<>"" THEN SYS "MessageTrans_Lookup",message_file%,token$ TO ,,message$
=message$

